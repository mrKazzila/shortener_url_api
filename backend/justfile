# =====================
# Base variables
# =====================
PROJECT_NAME := "shortener_url"
DC := "docker-compose"
DC_FILE := "docker-compose.yaml"
ENV_FILE := "env/.env"

# =====================
# Help (automatic)
# =====================
# Show available commands
help:
    @just --list


# =====================
# Environment Management
# =====================

# Configures the local environment (DB localhost:5433)
set_local_env:
    @sed -i '' 's/^DB_HOST=.*/DB_HOST=localhost/' env/.env
    @sed -i '' 's/^DB_PORT=.*/DB_PORT=5433/' env/.env
    @sed -i '' 's/^REDIS_HOST=.*/REDIS_HOST=localhost/' env/.env
    @echo "Environment set for LOCAL"

# Creates a requirements.txt file for dev
freeze-dev:
    @echo "Generating requirements.txt (dev)..."
    uv pip compile pyproject.toml --extra dev -o requirements.txt

# Creates a requirements.txt file for production
freeze-prod:
    @echo "Generating requirements.txt (prod)..."
    uv pip compile pyproject.toml -o requirements.txt

# Deletes requirements.txt
rm-requirements:
    @echo "Remove requirements.txt..."
    rm requirements.txt


# =====================
# Docker environment
# =====================

create-global-net:
    @docker network create global_net || true

# Configures environment variables for Docker
set_docker_env:
    @sed -i '' 's/^DB_HOST=.*/DB_HOST=postgres/' env/.env
    @sed -i '' 's/^DB_PORT=.*/DB_PORT=5432/' env/.env
    @sed -i '' 's/^REDIS_HOST=.*/REDIS_HOST=redis/' env/.env
    @echo "Environment set for DOCKER"

# Puts up containers kafka + postgres + redis
up:
    @echo "Starting containers..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} up -d --build kafka kafka-ui postgres redis
    @echo "Containers started"

# Stops containers and cleans volume
down:
    @echo "Stopping containers..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} down -v
    @echo "Containers stopped"


# =====================
# Kafka topics
# =====================

# Creates Kafka topics with the specified number of partitions
create_kafka_topics ARG:
    @echo "Creating Kafka topics with {{ARG}} partitions..."
    @docker exec kafka /opt/kafka/bin/kafka-topics.sh \
        --bootstrap-server kafka:9092 \
        --create --topic new_urls --partitions {{ARG}} --replication-factor 1 || true
    @docker exec kafka /opt/kafka/bin/kafka-topics.sh \
        --bootstrap-server kafka:9092 \
        --create --topic update_urls --partitions {{ARG}} --replication-factor 1 || true
    @echo "Kafka topics created."


# =====================
# Database migrations
# =====================

# Applies alembic migrations (upgrade head)
migrate-up:
    @echo "Running migrations..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} run --rm fastapi python -m alembic upgrade head

# Rolls back migration by one step
migrate-down:
    @echo "Downgrading migrations..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} run --rm fastapi python -m alembic downgrade -1


# =====================
# Application
# =====================

# Runs the fastapi application in docker
start-app:
    @echo "Starting app..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} up -d fastapi

# Redis CLI
redis-cli:
    @echo "Connecting to Redis CLI..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} exec redis redis-cli

# =====================
# Consumers
# =====================

# Scaling consumer and consumer_update
scale-consumers ARG:
    @echo "Scaling consumers to {{ARG}} replicas..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} up -d \
        --scale consumer={{ARG}} \
        --scale consumer_update={{ARG}}
    @echo "Consumers scaled."


# =====================
# Full startup
# =====================

# Full launch: env + deps + containers + themes + migrations + scaling + application
full-up partitions replicas:
    @echo "Running full-up..."
    @just create-global-net
    @just set_docker_env
    @just freeze-dev
    @just up
    @just migrate-up
    @just create_kafka_topics {{partitions}}
    @just scale-consumers {{replicas}}
    @just start-app
    @just rm-requirements
    @echo "Full startup completed."



restart-app:
    @just set_docker_env
    @just freeze-dev
    @echo "Restarting fastapi and consumer containers..."
    {{DC}} --env-file {{ENV_FILE}} -p {{PROJECT_NAME}} -f {{DC_FILE}} up -d --build fastapi consumer consumer_update
    @echo "FastAPI and consumers restarted"



# =====================
# Linters
# =====================

# Runs ruff (lint + format)
lint:
    @echo "Running linters..."
    - ruff check --fix --silent .
    - ruff format --silent .


# =====================
# Alembic: migrations creation
# =====================

# Create migration with autogeneration
make_migrate ARG:
    @echo "Create alembic revision: {{ARG}}"
    @alembic revision --autogenerate -m {{ARG}}

# Creates migration in the local environment
make_migrate_local ARG:
    @just set_local_env
    @just make_migrate {{ARG}}

# Rolls back to the specified revision locally
make_downgrade_local ARG:
    @just set_local_env
    @just make_downgrade {{ARG}}




MONITORING_DC_FILE := "docker/monitoring/docker-compose.yml"

# Starts monitoring stack
monitoring-up:
    @echo "Starting monitoring stack..."
    {{DC}} --env-file {{ENV_FILE}} -p monitoring_{{PROJECT_NAME}} -f {{MONITORING_DC_FILE}} up -d --build
    @echo "Monitoring stack started"

# Stops monitoring stack
monitoring-down:
    @echo "Stopping monitoring stack..."
    {{DC}} --env-file {{ENV_FILE}} -p monitoring_{{PROJECT_NAME}} -f {{MONITORING_DC_FILE}} down -v
    @echo "Monitoring stack stopped"

