# =====================
# Settings
# =====================
set shell := ["bash", "-eu", "-o", "pipefail", "-c"]


APP := "docker compose --env-file env/.env -p shortener-app -f docker-compose.yaml"
INFRA := "docker compose --env-file env/.env -p shortener-infra -f docker/infra/docker-compose.yml"
MON := "docker compose --env-file env/.env -p shortener-mon -f docker/monitoring/docker-compose.yml"

# =====================
# Aliases
# =====================
alias iu := infra-up
alias id := infra-down
alias ic := infra-clean

alias au := app-up
alias ad := app-down
alias ac := app-clean
alias ab := app-bootstrap

alias mu := mon-up
alias md := mon-down
alias mc := mon-clean

alias up := all-up
alias down := all-down

alias r := redis-cli


# =====================
# Help
# =====================
default:
    @just --list

help:
    @just --list


# =====================
# Quality
# =====================
lint:
    @echo "[ruff] Running linters..."
    - ruff check --fix --silent .
    - ruff format --silent .


# =====================
# Env switching
# =====================
# Set local env
env-local:
    @sed -i '' 's/^DB_HOST=.*/DB_HOST=localhost/' env/.env
    @sed -i '' 's/^DB_PORT=.*/DB_PORT=5433/' env/.env
    @sed -i '' 's/^REDIS_HOST=.*/REDIS_HOST=localhost/' env/.env
    @echo "[env] set for LOCAL"

# Set doker env
env-docker:
    @sed -i '' 's/^DB_HOST=.*/DB_HOST=postgres/' env/.env
    @sed -i '' 's/^DB_PORT=.*/DB_PORT=5432/' env/.env
    @sed -i '' 's/^REDIS_HOST=.*/REDIS_HOST=redis/' env/.env
    @echo "[env] set for DOCKER"


# =====================
# Requirements
# =====================
# Generating requirements.txt (dev)
req-dev:
    @echo "[req.txt] Generating requirements.txt (dev)..."
    @uv pip compile pyproject.toml --extra dev -o requirements.txt

# Generating requirements.txt (prod)
req-prod:
    @echo "[req.txt] Generating requirements.txt (prod)..."
    @uv pip compile pyproject.toml -o requirements.txt

# Removing requirements.txt
req-clean:
    @echo "[req.txt] Removing requirements.txt..."
    rm -f requirements.txt


# =====================
# Internal helpers
# =====================
_net-create:
    @if docker network inspect global_net >/dev/null 2>&1; then \
        echo "[global_net] already exists — skipping"; \
    else \
        echo "[global_net] creating..."; \
        docker network create global_net >/dev/null && echo "[global_net] created"; \
    fi

_pgbouncer-userlist-check:
    @if [ ! -f docker/infra/pgbouncer/userlist.txt ]; then \
        echo "[pgbouncer] Missing docker/infra/pgbouncer/userlist.txt"; \
        echo "[pgbouncer] Create it manually and fill it with user entries for pgbouncer"; \
        exit 1; \
    fi
    @echo "[pgbouncer] userlist.txt found — OK"


# =====================
# Kafka / scaling
# =====================
# Create kafka topics if not exist
kafka-topics partitions:
    @echo "[broker] Creating Kafka topics with {{partitions}} partitions..."
    @docker exec kafka /opt/kafka/bin/kafka-topics.sh \
        --bootstrap-server kafka:9092 --create --topic new-urls --partitions {{partitions}} --replication-factor 1 || true
    @docker exec kafka /opt/kafka/bin/kafka-topics.sh \
        --bootstrap-server kafka:9092 --create --topic update-urls --partitions {{partitions}} --replication-factor 1 || true
    @echo "[broker] Kafka topics created."

# Scaling kafka consumers
consumers-scale replicas:
    @echo "[app] Scaling consumers to {{replicas}} replicas..."
    {{APP}} up -d \
        --scale consumer={{replicas}} \
        --scale consumer-update={{replicas}}
    @echo "[app] Consumers scaled."


# =====================
# DB / migrations
# =====================
# Create alembic revision
db-rev msg:
    @echo "[db] Create alembic revision: {{msg}}"
    @alembic revision --autogenerate -m "{{msg}}"

# Generate alembic revision file
db-rev-local msg:
    @just env-local
    @just db-rev "{{msg}}"

# Downgrade alembic revision
db-down-local:
    @just env-local
    @alembic downgrade -1

# Upgrade DB megration to head
db-up:
    @echo "[db] upgrade head..."
    {{APP}} run --rm grpc python -m alembic upgrade head

# Downgrade DB megration
db-down:
    @echo "[db] downgrade -1..."
    {{APP}} run --rm grpc python -m alembic downgrade -1

# Apply alembic megration to DB
db-apply msg:
    @just env-docker
    @just db-up
    @just db-rev-local "{{msg}}"
    @just env-docker
    @just db-up


# =====================
# Stacks: infra / app / monitoring
# =====================

# Starting infra
infra-up:
    @echo "[infra] Starting..."
    @just _net-create
    @just _pgbouncer-userlist-check
    {{INFRA}} up -d --build
    @echo "[infra] Started!"

# Stopping (keeping volumes) infra
infra-down:
    @echo "[infra] Stopping (keeping volumes)..."
    {{INFRA}} down
    @echo "[infra] Stopped!"

# Stopping + removing volumes infra
infra-clean:
    @echo "[infra] Stopping + removing volumes..."
    {{INFRA}} down -v
    @echo "[infra] Cleaned!"

# Starting application
app-up:
    @echo "[app] Starting..."
    @just _net-create
    @just env-docker
    {{APP}} up -d --build
    @echo "[app] Started!"

# Starting fastapi only
app-up-api:
    @echo "[app] Starting fastapi only..."
    @just _net-create
    @just env-docker
    {{APP}} up -d fastapi
    @echo "[app] fastapi started!"

# Rebuild application
app-rebuild:
    @just env-docker
    @just req-dev
    @echo "[app] Rebuilding fastapi + consumers..."
    {{APP}} up -d --build grpc consumer consumer-update
    @just req-clean
    @echo "[app] Rebuilt."

# Bootstrapping application
app-bootstrap partitions replicas:
    @echo "[app] Bootstrapping..."
    @just _net-create
    @just env-docker
    @just req-dev
    @just db-up
    @just kafka-topics {{partitions}}
    @just consumers-scale {{replicas}}
    @just app-up-api
    @just req-clean
    @echo "[app] Bootstrap completed."

# Stopping (keeping volumes) application
app-down:
    @echo "[app] Stopping (keeping volumes)..."
    {{APP}} down
    @echo "[app] Stopped!"

# Stopping + removing volumes application
app-clean:
    @echo "[app] Stopping + removing volumes..."
    {{APP}} down -v
    @echo "[app] Cleaned!"

# Starting monitoring
mon-up:
    @echo "[mon] Starting..."
    {{MON}} up -d --build
    @echo "[mon] Started!"

# Stopping (keeping volumes) monitoring
mon-down:
    @echo "[mon] Stopping (keeping volumes)..."
    {{MON}} down
    @echo "[mon] Stopped!"

# Stopping + removing volumes monitoring
mon-clean:
    @echo "[mon] Stopping + removing volumes..."
    {{MON}} down -v
    @echo "[mon] Cleaned!"


# =====================
# All stacks
# =====================

# Starting all
all-up:
    @just infra-up
    @just app-up
    @just mon-up
    @echo "[all] up"

# Stopping all
all-down:
    @just mon-down
    @just app-down
    @just infra-down
    @echo "[all] down"


# =====================
# Convenience
# =====================

# Run redis CLI
redis-cli:
    @echo "[redis] Connecting..."
    {{INFRA}} exec redis redis-cli
